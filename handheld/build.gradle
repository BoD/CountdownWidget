apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
if (buildProperties.enableFabric.toBoolean()) apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId 'org.jraf.android.countdownwidget3'
        minSdkVersion 14
        targetSdkVersion versions.targetSdk
        versionCode buildNumber
        versionName buildProperties.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
        // For now we enable debug logs all the configs
        buildConfigField 'boolean', 'DEBUG_LOGS', 'true'

        resConfigs 'en', 'fr'

        // Useful for api keys in the manifest (Maps, Crashlytics, ...)
        manifestPlaceholders = buildProperties

        // Setting this to false enables the png generation at buildtime
        // (see http://android-developers.blogspot.fr/2016/02/android-support-library-232.html)
        vectorDrawables.useSupportLibrary = false
    }

    flavorDimensions 'blur', 'movie'

    productFlavors {
        episode8 {
            dimension 'movie'

            buildConfigField 'org.jraf.android.countdownwidget.util.Movie', 'MOVIE', 'org.jraf.android.countdownwidget.util.Movie.EPISODE_VIII'
        }

        trump {
            dimension 'movie'

            buildConfigField 'org.jraf.android.countdownwidget.util.Movie', 'MOVIE', 'org.jraf.android.countdownwidget.util.Movie.TRUMP'
        }

        hansolo {
            dimension 'movie'

            buildConfigField 'org.jraf.android.countdownwidget.util.Movie', 'MOVIE', 'org.jraf.android.countdownwidget.util.Movie.HAN_SOLO'
        }


        blurred {
            dimension 'blur'
        }

        clear {
            dimension 'blur'
        }
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signingStoreFile)
            storePassword buildProperties.signingStorePassword
            keyAlias buildProperties.signingKeyAlias
            keyPassword buildProperties.signingKeyPassword
        }
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'GIT_SHA1', "\"dev\""
            buildConfigField 'String', 'BUILD_DATE', "\"dev\""
            buildConfigField 'boolean', 'CRASH_REPORT', 'false'
        }

        release {
            buildConfigField 'String', 'GIT_SHA1', "\"${gitSha1}\""
            buildConfigField 'String', 'BUILD_DATE', "\"${buildDate}\""
            buildConfigField 'boolean', 'CRASH_REPORT', 'true'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError true
        textReport true
        ignoreWarnings true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

android.applicationVariants.all { variant ->
    // Define application id based on flavors
    switch (variant.flavorName) {
    // Episode VIII
        case "blurredEpisode8":
            variant.mergedFlavor.setApplicationId('org.jraf.android.countdownwidget3.e8.blurred')
            break

        case "clearEpisode8":
            variant.mergedFlavor.setApplicationId('org.jraf.android.countdownwidget3.e8.clear')
            break

    // Trump
        case "clearTrump":
            variant.mergedFlavor.setApplicationId('org.jraf.android.countdownwidget3.trump.clear')
            break

    // Han Solo
        case "blurredHansolo":
            variant.mergedFlavor.setApplicationId('org.jraf.android.countdownwidget3.hs.blurred')
            break

        case "clearHansolo":
            variant.mergedFlavor.setApplicationId('org.jraf.android.countdownwidget3.hs.clear')
            break
    }

    // Create new copy tasks, for release builds
    if (variant.buildType.name == 'release') {
        variant.outputs.each { output ->
            def outputFile = file("build/outputs/apk/${variant.flavorName}/release/$output.outputFileName")
            def apkName = "${globalProjectName}-${project.name}-${android.defaultConfig.versionCode}-${variant.flavorName}-signed.apk"

            // Copy the apk to the 'etc' folder
            def copyApkToEtc = tasks.create("copy${variant.name.capitalize()}ApkToEtc", Copy)
            copyApkToEtc.from(outputFile)
            copyApkToEtc.into('../etc/apk')
            copyApkToEtc.rename output.outputFileName, apkName

            // Copy the apk to the deploy folder
            def copyApkToDeploy = tasks.create("copy${variant.name.capitalize()}ApkToDeploy", Copy)
            copyApkToDeploy.from(outputFile)
            copyApkToDeploy.into(buildProperties.deployFolder)
            copyApkToDeploy.rename output.outputFileName, apkName

            // Make the copy tasks run after the assemble tasks of the variant
            variant.assemble.finalizedBy(copyApkToEtc, copyApkToDeploy)
        }
    }
}

android.variantFilter { variant ->
    def flavorNames = variant.getFlavors().collect({ it.name })
    if (flavorNames.contains("blurred") && flavorNames.contains("trump")) {
        variant.setIgnore(true)
    }
}

dependencies {
    // Tests
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
    testImplementation "org.easytesting:fest-assert-core:$versions.fest"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"

    // Support library
    implementation "com.android.support:preference-v7:$versions.supportLibrary"
    implementation "com.android.support:preference-v14:$versions.supportLibrary"

    // Play Services
    implementation "com.google.android.gms:play-services-gcm:$versions.playServices"

    // Fabric / Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics@aar") { transitive = true }

    // JRAF
//    implementation 'com.github.BoD:jraf-android-util:-SNAPSHOT'
    implementation 'org.jraf:jraf-android-util:1.0.0'
    kapt "org.jraf:prefs-compiler:$versions.prefs"
    implementation "org.jraf:prefs:$versions.prefs"

    // Necessary to have Kotlin and dataBinding at the same time
    kapt "com.android.databinding:compiler:$versions.androidGradlePlugin"
}

// See https://kotlinlang.org/docs/reference/kapt.html
kapt {
    correctErrorTypes = true
}